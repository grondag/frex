import net.fabricmc.loom.task.RemapJarTask

plugins {
	id 'fabric-loom' version '0.2.4-SNAPSHOT'
	id 'java'
	id 'maven'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '4.0.3'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'origin/master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1;
    }
}

archivesBaseName = "frex"
group = "grondag"
version = "0.7.3+build." + project.getVersionCode();

minecraft {
}

processResources {
	inputs.property "version", version
	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": version
	}
}

configurations {
    included {
        transitive = false
    }
}

shadowJar {
    version = version
    classifier = null
    configurations = [project.configurations.included]
}

task remapShadowJar(type: RemapJarTask) {
    input = shadowJar.archivePath
    output = shadowJar.archivePath
}

tasks.remapShadowJar.dependsOn tasks.shadowJar

tasks.assemble.dependsOn tasks.remapShadowJar

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

jar {
	classifier = "slim"
	from "LICENSE"
}

sourceSets {
	main {
	        java {
	            include 'grondag/**/*'
	            exclude '*.DS_Store'
	        }
	        resources
	        {
	        	include '**/*'
	        	exclude '*.DS_Store'
	        }
	    }
    test {
        java {
            exclude 'grondag/**'
        }
    }
}

def mcVersion = "1.14.3"

dependencies {
	minecraft "com.mojang:minecraft:$mcVersion"
	mappings "net.fabricmc:yarn:$mcVersion+build.1"
    modImplementation "net.fabricmc:fabric-loader:0.4.8+build.155"
    modCompile "net.fabricmc.fabric-api:fabric-api:0.3.0+build.187"
	
	compile "org.apiguardian:apiguardian-api:1.0.0"
	compile "com.google.code.findbugs:jsr305:3.0.2"
	
	compile "org.joml:joml:1.9.15"
	included "org.joml:joml:1.9.15"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
			artifact(jar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
			artifact(shadowJar) {
				builtBy remapShadowJar
			}
        }
    }

    repositories {
        maven {
            url "https://grondag-repo.appspot.com"
            credentials {
				if (project.hasProperty('maven_user')){
					username = project.getProperty('maven_user')
				}
				if (project.hasProperty('maven_password')){
					password = project.getProperty('maven_password')
				}
			}
        }
    }
}